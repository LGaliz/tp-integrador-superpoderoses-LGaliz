import superpoderoses.*


describe "Superpoderoses" {
	
	/* ACLARACIÓN 
	 * El test asume todos los objetos como instancias de clases. 
	 * Si el diseño de la solución utiliza objetos bien conocidos en algunos casos entonces 
	 * se debe remover la declaración de la variable y la línea en que se sugiere la instanciación.
	 */
	// Punto 1
	// Poderes
	const altaVelocidad = new Velocidad(rapidez = 2)
	const superVelocidad = new Velocidad(rapidez = 3)
	const vueloRasante = new Vuelo(alturaMax = 10, energiaDespegue = 10)
	const vueloAlto = new Vuelo(alturaMax = 300, energiaDespegue = 200) //es inmune R
	const vueloRasanteAmplificado = new PoderAmplificado(poderBase = vueloRasante, numeroAmplificador = 4)
	const superVelocidadAmplificada = new PoderAmplificado(poderBase = superVelocidad, numeroAmplificador = 3)
	
		// Personajes 
	const capitanaPatriaGrande = new Personaje(estrategia = 3, espiritualidad = 5, poderes = [superVelocidadAmplificada, vueloAlto])
	const plusvaliaCero = new Personaje(estrategia = 2, espiritualidad = 4, poderes = [altaVelocidad, vueloRasanteAmplificado])
	const usinaDeDerechos = new Personaje(estrategia = 1, espiritualidad = 6, poderes = [vueloRasante])
	
	// Punto 2
	// Equipo
	const cooperativaJusticiera = new Equipo(personajes = [capitanaPatriaGrande, plusvaliaCero, usinaDeDerechos])
	
	// Punto 3 
	// Peligros
	const peligroSencillo = new Peligro(capacidadDeBatalla = 30, desechosRadiactivos = false, nivelDeComplejidad = 1, personajesSimultaneos = 2)
	const peligroSencilloRadiactivo = new Peligro(capacidadDeBatalla = 30, desechosRadiactivos = true, nivelDeComplejidad = 1, personajesSimultaneos = 2)
	const peligroModerado = new Peligro(capacidadDeBatalla = 50, desechosRadiactivos = true, nivelDeComplejidad = 2, personajesSimultaneos = 1)
	const peligroAlto = new Peligro(capacidadDeBatalla = 400, desechosRadiactivos = true, nivelDeComplejidad = 3, personajesSimultaneos = 2)
	
	// Punto 4
	// Metahumanos
	const fragmentadorMonopolios = new Metahumano(estrategia = 1, espiritualidad = 6, poderes = [vueloRasante])
	const educadorPublico = new Mago(poderAcumulado = 12, estrategia = 1, espiritualidad = 6, poderes = [vueloRasante])
	
	/* ACLARACIÓN
	 * Los tests están comentados para evitar errores y warnings, así poder ir incorporándolos 
	 * a medida que se avanza con la solución del ejercicio
	 */
	
	 test "punto 1.1 capacidad de batalla que aporta un poder" {
		// Verificar que la capacidad de batalla que le aporta superVelocidadAmplificada a capitanaPatriaGrande es 576
		assert.equals(576, superVelocidadAmplificada.capacidadDeBatalla(capitanaPatriaGrande))
		// Verificar que la capacidad de batalla que le aporta vueloAlto a capitanaPatriaGrande es 876
		assert.equals(876, vueloAlto.capacidadDeBatalla(capitanaPatriaGrande))
		// Verificar que la capacidad de batalla que le aporta altaVelocidad a plusvaliaCero es 72
		assert.equals(72, altaVelocidad.capacidadDeBatalla(plusvaliaCero))
		// Verificar que la capacidad de batalla que le aporta vueloRasanteAmplificado a plusvaliaCero es 144
		assert.equals(144, vueloRasanteAmplificado.capacidadDeBatalla(plusvaliaCero))
		// Verificar que la capacidad de batalla que le aporta vueloRasante a usinaDeDerechos es 49
		assert.equals(49, vueloRasante.capacidadDeBatalla(usinaDeDerechos))
	}

	test "punto 1.2 capacidad de batalla de un personaje" {
		// Verificar que la capacidad de batalla de capitanaPatriaGrande es 1452
		assert.equals(1452, capitanaPatriaGrande.capacidadDeBatalla())
		// Verificar que la capacidad de batalla de plusvaliaCero es 216
		assert.equals(216, plusvaliaCero.capacidadDeBatalla())
		// Verificar que la capacidad de batalla de usinaDeDerechos es 49
		assert.equals(49, usinaDeDerechos.capacidadDeBatalla())
	}

	test "punto 2.1 Saber el miembro vulnerable de un equipo" {
		// Verificar que miembro mas vulnerable de cooperativaJusticiera es usinaDeDerechos
		assert.equals(usinaDeDerechos, cooperativaJusticiera.miembroMasVulnerable())
	}

	test "punto 2.2 Saber la calidad de un equipo" {
		// Verificar que la calidad de cooperativaJusticiera es 572.33333
		assert.equals((1452 + 216 + 49) / 3, cooperativaJusticiera.calidad())
	}

	test "punto 2.3 Saber los mejores poderes de un equipo" {
		// Verificar que los mejores poderes de cooperativaJusticiera son vueloAlto, vueloRasanteAmplificado y vueloRasante
		const poderesEsperados = [vueloAlto, vueloRasanteAmplificado, vueloRasante]
		assert.equals(poderesEsperados, cooperativaJusticiera.mejoresPoderes())
	}
	
	test "punto 3.1 personajes contra el peligro" {
		// Verificar que peligroSencillo puede ser afrontado por capitanaPatriaGrande
		assert.that(capitanaPatriaGrande.puedeEnfrentar(peligroSencillo))
		// Verificar que peligroSencillo puede ser afrontado por plusvaliaCero
		assert.that(plusvaliaCero.puedeEnfrentar(peligroSencillo))
		// Verificar que peligroSencillo puede ser afrontado por usinaDeDerechos
		assert.that(usinaDeDerechos.puedeEnfrentar(peligroSencillo))

		// Verificar que peligroSencilloRadiactivo puede ser afrontado por capitanaPatriaGrande
		assert.that(capitanaPatriaGrande.puedeEnfrentar(peligroSencilloRadiactivo))
		// Verificar que peligroSencilloRadiactivo puede ser afrontado por plusvaliaCero
		assert.that(plusvaliaCero.puedeEnfrentar(peligroSencilloRadiactivo))
		// Verificar que peligroSencilloRadiactivo NO puede ser afrontado por usinaDeDerechos
		assert.notThat(usinaDeDerechos.puedeEnfrentar(peligroSencilloRadiactivo))

		// Verificar que peligroModerado puede ser afrontado por capitanaPatriaGrande
		assert.that(capitanaPatriaGrande.puedeEnfrentar(peligroModerado))
		// Verificar que peligroModerado puede ser afrontado por plusvaliaCero
		assert.that(plusvaliaCero.puedeEnfrentar(peligroModerado))
		// Verificar que peligroModerado NO puede ser afrontado por usinaDeDerechos
		assert.notThat(usinaDeDerechos.puedeEnfrentar(peligroModerado))
	
		// Verificar que peligroAlto puede ser afrontado por capitanaPatriaGrande
		assert.that(capitanaPatriaGrande.puedeEnfrentar(peligroAlto))
		// Verificar que peligroAlto NO puede ser afrontado por plusvaliaCero
		assert.notThat(plusvaliaCero.puedeEnfrentar(peligroAlto))
		// Verificar que peligroAlto NO puede ser afrontado por usinaDeDerechos
		assert.notThat(usinaDeDerechos.puedeEnfrentar(peligroAlto))
	}
	
	test "punto 3.2 Saber si un peligro es sensato " {
		// Verificar que peligroSencillo es sensato para cooperativaJusticiera
		assert.that(cooperativaJusticiera.peligroSensato(peligroSencillo))
		// Verificar que peligroSencilloRadiactivo NO es sensato para cooperativaJusticiera
		assert.notThat(cooperativaJusticiera.peligroSensato(peligroSencilloRadiactivo))
		// Verificar que peligroModerado NO es sensato para cooperativaJusticiera
		assert.notThat(cooperativaJusticiera.peligroSensato(peligroModerado))
		// Verificar que peligroAlto NO es sensato para cooperativaJusticiera
		assert.notThat(cooperativaJusticiera.peligroSensato(peligroAlto))
	}
	
	test "punto 4.1 Afrontar peligro individualmente: OK" {
		// Hacer que usinaDeDerechos afronte a peligroSencillo
		usinaDeDerechos.enfrentar(peligroSencillo)
		// Verificar que la estrategia de usinaDeDerechos es 2
		assert.equals(2, usinaDeDerechos.estrategia())
	}

	test "punto 4.1 Afrontar peligro individualmente: no OK por radioactividad" {
		// Hacer que usinaDeDerechos afronte a peligroSencilloRadiactivo (NO SE PUEDE!)
		assert.throwsException({ usinaDeDerechos.enfrentar(peligroSencilloRadiactivo) })
		// Verificar que la estrategia de usinaDeDerechos es 1
		assert.equals(1, usinaDeDerechos.estrategia())
	}
	
	test "punto 4.1 Afrontar peligro individualmente: no OK por capacidad de batalla" {
		// Hacer que usinaDeDerechos afronte a peligroModerado (NO SE PUEDE!)
		assert.throwsException({ usinaDeDerechos.enfrentar(peligroModerado) })
		// Verificar que la estrategia de usinaDeDerechos es 1
		assert.equals(1, usinaDeDerechos.estrategia())
	}
	
	test "punto 4.2  Afrontar peligro (moderado) en equipo  " {
		// Hacer que cooperativaJusticiera afronte a peligroModerado
		cooperativaJusticiera.enfrentar(peligroModerado)
		// Verificar que la estrategia de capitanaPatriaGrande es 5
		assert.equals(5, capitanaPatriaGrande.estrategia())
		// Verificar que la estrategia de plusvaliaCero es 4
		assert.equals(4, plusvaliaCero.estrategia())
		// Verificar que la estrategia de usinaDeDerechos es 1
		assert.equals(1, usinaDeDerechos.estrategia())
	}

	test "punto 4.2  Afrontar peligro (alto) en equipo (no ok)" {
		// Hacer que cooperativaJusticiera afronte a peligroAlto (NO SE PUEDE!)
		assert.throwsException({ cooperativaJusticiera.enfrentar(peligroAlto) })
		// Verificar que la estrategia de capitanaPatriaGrande es 3
		assert.equals(3, capitanaPatriaGrande.estrategia())
		// Verificar que la estrategia de plusvaliaCero es 2
		assert.equals(2, plusvaliaCero.estrategia())
		// Verificar que la estrategia de usinaDeDerechos es 1
		assert.equals(1, usinaDeDerechos.estrategia())
	}

	test "punto 5.1  Metahumano " {
		// Verificar que la capacidad de batalla de fragmentadorMonopolios es 98
		assert.equals(98, fragmentadorMonopolios.capacidadDeBatalla())
		// Verificar que peligroSencilloRadiactivo puede ser afrontado por fragmentadorMonopolios
		assert.that(fragmentadorMonopolios.puedeEnfrentar(peligroSencilloRadiactivo))
		// Hacer que fragmentadorMonopolios afronte a peligroSencilloRadiactivo
		fragmentadorMonopolios.enfrentar(peligroSencilloRadiactivo)
		// Verificar que la estrategia de fragmentadorMonopolios es 2
		assert.equals(2, fragmentadorMonopolios.estrategia())
		// Verificar que la espiritualidad de fragmentadorMonopolios es 7
		assert.equals(7, fragmentadorMonopolios.espiritualidad())
		// Verificar que peligroAlto NO puede ser afrontado por fragmentadorMonopolios
		assert.notThat(fragmentadorMonopolios.puedeEnfrentar(peligroAlto))
		// Hacer que fragmentadorMonopolios afronte a peligroAlto (NO SE PUEDE!)
		assert.throwsException({ fragmentadorMonopolios.enfrentar(peligroAlto) })
	}

	test "punto 5.2  Mago " {
		// Verificar que la capacidad de batalla de educadorPublico es 110
		assert.equals(110, educadorPublico.capacidadDeBatalla())
		// Verificar que peligroAlto NO puede ser afrontado por educadorPublico
		assert.notThat(educadorPublico.puedeEnfrentar(peligroAlto))
		// Hacer que educadorPublico afronte a peligroAlto (NO SE PUEDE!)
		assert.throwsException({ educadorPublico.enfrentar(peligroAlto) })
		
		// Verificar que peligroSencilloRadiactivo puede ser afrontado por educadorPublico
		assert.that(educadorPublico.puedeEnfrentar(peligroSencilloRadiactivo))
		// Hacer que educadorPublico afronte a peligroSencilloRadiactivo
		educadorPublico.enfrentar(peligroSencilloRadiactivo)
		// Verificar que la estrategia de educadorPublico es 2
		assert.equals(2, educadorPublico.estrategia())
		// Verificar que la espiritualidad de educadorPublico es 7
		assert.equals(7, educadorPublico.espiritualidad())
		// Verificar que el poder acumulado de educadorPublico es 7
		assert.equals(7, educadorPublico.poderAcumulado())
		
		// Hacer que educadorPublico afronte a peligroSencilloRadiactivo
		educadorPublico.enfrentar(peligroSencilloRadiactivo)
		// Verificar que la estrategia de educadorPublico es 2
		assert.equals(2, educadorPublico.estrategia())
		// Verificar que la espiritualidad de educadorPublico es 7
		assert.equals(7, educadorPublico.espiritualidad())
		// Verificar que el poder acumulado de educadorPublico es 2
		assert.equals(2, educadorPublico.poderAcumulado())
	}

}